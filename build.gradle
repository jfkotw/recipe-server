plugins {
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id "org.openapi.generator" version "4.3.0"
    id 'java'
}

group 'me.jonfuller.recipe'
version '1.0-SNAPSHOT'

sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


ext {
    swagger_annotations_version = "1.5.22"
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web:2.2.6.RELEASE') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }

    implementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot2:1.5'
    implementation 'io.symphonia:lambda-logging:1.0.1'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.0'

    implementation platform('software.amazon.awssdk:bom:2.11.4')

    implementation 'software.amazon.awssdk:dynamodb'
    implementation 'software.amazon.awssdk:lambda'
    implementation 'software.amazon.awssdk:url-connection-client'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "io.swagger:swagger-annotations:$swagger_annotations_version"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }


//    implementation 'com.amazonaws:aws-lambda-java-core:1.2.0'
//    implementation 'com.amazonaws:aws-lambda-java-events:2.2.7'
//    runtimeOnly 'com.amazonaws:aws-lambda-java-log4j2:1.1.0'


}

test {
    useJUnitPlatform()
}

/* Generate Server Code from OpenAPI definition */
def recipeApiYml = "$rootDir/src/main/resources/recipe-api.yml".toString()

/* server code configuration */
task buildRecipeServerCode(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "spring"
    inputSpec = recipeApiYml
    outputDir = "$buildDir/generated".toString()

    groupId = "$project.group"
    id = "$project.name-java-client"
    version = "$project.version"
    apiPackage = "me.jonfuller.recipe.api"
    modelPackage = "me.jonfuller.recipe.api.model"
    enablePostProcessFile = true
    skipOverwrite = false
    configOptions = [
            configPackage       : "me.jonfuller.recipe.api.config",
            java8               : "true",
            dateLibrary         : "java8",
            serializationLibrary: "jackson",
            library             : "spring-boot",
            useBeanValidation   : "true",
            interfaceOnly       : "true",
            serializableModel   : "true",
            useTags             : "true"
    ]
}

// code will be generated during compilation
compileJava.dependsOn(buildRecipeServerCode)

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from(configurations.compileClasspath) {
            exclude 'tomcat-embed-*'
        }
    }
}

build.dependsOn buildZip

// attach the generated folder as source for this project
sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}